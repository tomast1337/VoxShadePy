?start: statement+

?statement: if_stmt
          | return_stmt
          | assign_stmt
          | expr_stmt

if_stmt: "if" expr block
block: "{" statement* "}"

return_stmt: "return" expr ";"
assign_stmt: NAME "=" expr ";"
expr_stmt: expr ";"

?expr: comp_expr

?comp_expr: arith_expr (comp_op arith_expr)*

?arith_expr: term_expr (add_op term_expr)*

?term_expr: factor (mul_op factor)*

?factor: func_call
        | const_atom
        | var_atom
        | material_atom
        | NUMBER
        | "(" expr ")"

func_call: ("sin"|"cos"|"noise") "(" [expr ("," expr)*] ")"

var_atom: NAME -> var
const_atom: "x" -> const_var
          | "y" -> const_var 
          | "z" -> const_var
          | "time" -> const_var

material_atom: MATERIAL -> material

MATERIAL: "air"|"stone"|"water"|"grass"|"crystal"
         |"sand"|"wood"|"metal"|"lava"|"ice"
         |"dirt"|"snow"|"cloud"|"glass"|"brick"|"obsidian"

comp_op: "=="|"!="|"<"|">"|"<="|">="
add_op: "+"|"-"
mul_op: "*"|"/"

NAME: /[a-zA-Z_][a-zA-Z0-9_]*/
NUMBER: /-?\d+(\.\d+)?/

%ignore /\s+/
%ignore /\/\/[^\n]*/